MODULE RNL__subscription(SYSMODULE)

    !This is the reference for all datatype/object modules. 
    !To create a new datatype copy this module and ctrl+f replace
    !"subscription" With the new datatype, set the NULL value to a value
    !that will never be in use, and adjust the list{X} size

    !Datatype: subscription
    !Module with standard methods for the datatype

    !***************************************************************************
    ! Data
    !***************************************************************************

    RECORD subscription
        eventPriority priority;

        event triggerEvent;
        dataPointer triggerObject;

        string reciverProcName;
        dataPointer reciverObject;

        event aliasEvent;
        dataPointer aliasObject;
    ENDRECORD

    !A constant that can be searched for to determine if a datatype module is loaded
    PERS dataType dataType_subscription:="subscription";

    !Per module setting for whether the list should be used
    !IF this bool is set to false (default) then the NEW keyword will
    !will create a stirng value datapointer for this datatype.
    !If it is set to true, then the NEW keyword will use the pre declared list.
    LOCAL CONST bool USE_LIST:=TRUE;

    !INHERITS_FROM is the type that this type inherits from.
    ! "" signifies no inheritance
    LOCAL CONST dataType INHERITS_FROM:="";

    !Null constants
    CONST subscription subscription_NULL:=[0,[0],["","",0,"","",0,""],"",["","",0,"","",0,""],[0],["","",0,"","",0,""]];
    LOCAL CONST subscription subscription_DEFAULT:=[0,[0],["","",0,"","",0,""],"event_handler",["","",0,"","",0,""],[0],["","",0,"","",0,""]];

    !Pre delcared list to be dynamically allocated
    LOCAL VAR subscription list{500};
    LOCAL VAR num list_lenght:=0;

    !###########################################################################
    ! NEW Constructor and ERASE Destructor
    !###########################################################################

    !NEW constructor with initial values
    FUNC dataPointer NEW_subscription(
        \eventPriority priority,
        \dataPointer triggerObject,
        \INOUT event triggerEvent,
        \dataPointer reciverObject,
        \string reciverProcName,
        \INOUT event aliasEvent,
        \dataPointer aliasObject
        )
        VAR dataPointer pointer;
        VAR subscription data;

        data:=subscription_DEFAULT;

        !Initialize events
        IF Present(triggerEvent) event_setup triggerEvent;
        IF Present(aliasEvent) event_setup aliasEvent;

        !Add initial data
        IF Present(priority) data.priority:=priority;
        IF Present(triggerEvent) data.triggerEvent:=triggerEvent;
        IF Present(triggerObject) data.triggerObject:=triggerObject;
        IF Present(reciverProcName) data.reciverProcName:=reciverProcName;
        IF Present(reciverObject) data.reciverObject:=reciverObject;
        IF Present(aliasEvent) data.aliasEvent:=aliasEvent;
        IF Present(aliasObject) data.aliasObject:=aliasObject;

        !Allocate and set data
        NEW pointer;
        subscription_set pointer,data;
        RETURN pointer;
    ENDFUNC

    !NEW constructor to allocate a data and return pointer
    LOCAL PROC NEW(INOUT dataPointer pointer\dataPointer self)
        VAR subscription data;
        VAR dataPointer self_;
        VAR dataPointer inheritanceParent;
        VAR dataPointer inheritanceParent_Pointer;

        self_:=pointer;
        IF Present(self) self_:=self;

        IF USE_LIST THEN
            !Allocate a list position
            pointer.type:=Type(list);
            LIST_subscription_append list\lenght:=list_lenght,data\index:=pointer.id;

            IF NOT INHERITS_FROM="" THEN
                !Create nessesary inheritance chain
                inheritanceParent:=NEW_(INHERITS_FROM);
                inheritanceParent_Pointer:=NEW_("dataPointer");
                dataPointer_set inheritanceParent_Pointer,inheritanceParent;
                pointer.inheritanceParent_id:=inheritanceParent_Pointer.id;
                pointer.inheritanceParent_name:=inheritanceParent_Pointer.name;
            ENDIF

        ELSE
            !Create a string value datatype
            pointer:=subscription_(subscription_DEFAULT);
        ENDIF

        subscription_set pointer,subscription_DEFAULT;

    ERROR
        RAISE ;
    ENDPROC

    !Resolve an RMQ_execute command for this datatype. Return result
    LOCAL PROC RMQ_execute(INOUT dataPointer pointer,VAR rmqmessage message)
        VAR subscription data;
        RMQGetMsgData message,data;

        !Add custom behavior for "executing" this datatype here
        pointer:=dataPointer_NULL;

    ERROR
        RAISE ;
    ENDPROC

    !Allocate a data, set the data to recived RMQ message, and return pointer
    LOCAL PROC RMQ_NEW(INOUT dataPointer pointer,VAR rmqmessage message)

        VAR subscription data;
        RMQGetMsgData message,data;

        IF USE_LIST THEN
            NEW pointer;
            subscription_set pointer,data;
            RETURN ;
        ENDIF

        pointer:=subscription_(data);

    ERROR
        RAISE ;
    ENDPROC

    !Send RMQ message of this datatype
    LOCAL PROC RMQ_send(INOUT dataPointer data,dataPointer queue,dataPointer code)
        VAR subscription data_;
        VAR string queue_;
        VAR num code_;

        VAR rmqslot slot;

        data_:=subscription_get(data);
        queue_:=string_get(queue);
        code_:=num_get(code);

        rmqfindslot slot,queue_;
        RMQSendMessage slot,data_\UserDef:=code_;

    ERROR
        RAISE ;
    ENDPROC

    !Cleans up any datapointer
    LOCAL PROC ERASE(INOUT dataPointer pointer)

        VAR dataPointer inheritanceParent;

        !Call ERASE on the object that this object inherits from
        IF pointer.inheritanceParent_name<>""
            OR pointer.inheritanceParent_id>0 THEN
            inheritanceParent.id:=pointer.inheritanceParent_id;
            inheritanceParent.name:=pointer.inheritanceParent_name;

            ERASE_ inheritanceParent;
        ENDIF

        !ERASE
        IF dataPointer_isReference(pointer) subscription_set pointer,subscription_NULL;
        IF dataPointer_isIndex(pointer) LIST_subscription_del list\lenght:=list_lenght,pointer.id;
        pointer:=dataPointer_NULL;

    ERROR
        RAISE ;
    ENDPROC

    !///////////////////////////////////////////////////////////////////////////
    ! Privat Data Methods
    !///////////////////////////////////////////////////////////////////////////

    !¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤
    ! WRAPPER Methods
    !¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤¤

    !===========================================================================
    ! Public Data/Object Methods
    !===========================================================================

    !---------------------------------------------------------------------------
    ! Auxiliary Methods
    !---------------------------------------------------------------------------

    FUNC num RNL__subscription_listLenght()
        RETURN list_lenght; 
    ENDFUNC

    FUNC subscription RNL__subscription_list(num i)
        RETURN list{i};
    ENDFUNC
    

    !&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
    ! Getter and Setter Methods
    !&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

    !%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
    ! Standardized Methods
    !%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

    !%%%% DataPointer methods %%%%

    !event_handler is the default target for events subscribed to this object
    LOCAL PROC event_handler(dataPointer pointer,dataPointer eventNote)
        !Add default event handeling here
    ENDPROC

    !%%%% DataPointer methods %%%%

    !Set a datapointers value
    PROC subscription_set(INOUT dataPointer pointer,subscription value)
        IF dataPointer_isReference(pointer) SetDataVal pointer.name,value;
        IF dataPointer_isReference(pointer) RETURN;
        IF dataPointer_isValue(pointer) pointer.value:=ValToStr(value);
        IF dataPointer_isValue(pointer) RETURN;
        IF dataPointer_isIndex(pointer) list{pointer.id}:=value;
        IF dataPointer_isIndex(pointer) RETURN;
        !ERROR
        !Data Pointer Not set up
        errno_bookAndRaise ERR_BAD_DATA_POINTER;
    ERROR
        RAISE ;
    ENDPROC

    !Get a datapointers value
    FUNC subscription subscription_get(dataPointer pointer)
        VAR subscription returnValue;
        returnValue:=subscription_NULL;
        IF dataPointer_isReference(pointer) GetDataVal pointer.name,returnValue;
        IF dataPointer_isValue(pointer) errFromBool StrToVal(pointer.value,returnValue),ERR_SYM_ACCESS;
        IF dataPointer_isIndex(pointer) returnValue:=list{pointer.id};
        RETURN returnValue;
    ERROR
        RAISE ;
    ENDFUNC

    !Create a value-datapointer from a value, or that points to a value
    FUNC dataPointer subscription_(subscription value\switch asReference)
        VAR dataPointer pointer;
        pointer.type:=Type(value);
        IF Present(asReference) pointer.name:=ArgName(value);
        IF NOT Present(asReference) pointer.value:=ValToStr(value);
        RETURN pointer;
    ERROR
        RAISE ;
    ENDFUNC

    ! %%%% LIST methods %%%%

    !Initialize new list
    PROC LIST_subscription_NEW(
        INOUT subscription list{*}
        \INOUT num lenght)

        FOR i FROM 1 TO Dim(list,1) DO
            list{i}:=subscription_NULL;
        ENDFOR

        lenght:=0;

    ENDPROC

    !Append a datapointer to the list
    !The optional argument index will be set to the index that was used
    PROC LIST_subscription_append(
        INOUT subscription list{*}
        \INOUT num lenght,
        subscription item
        \INOUT num index)

        IF NOT Present(lenght) THEN
            FOR i FROM 1 TO Dim(list,1) DO
                IF list{i}=subscription_NULL THEN
                    list{i}:=item;
                    IF Present(index) index:=i;
                    RETURN ;
                ENDIF
            ENDFOR
            RETURN ;
        ENDIF

        lenght:=lenght+1;
        list{lenght}:=item;
        index:=lenght;

    ERROR
        RAISE ;
    ENDPROC

    !Pop the last data pointer in the list
    !Or if index is specified, pops the index
    FUNC subscription LIST_subscription_pop(
        INOUT subscription list{*}
        \INOUT num lenght,
        \num index)

        VAR subscription item;

        !Pop at spesific index
        IF Present(index) THEN
            item:=list{index};
            list{index}:=subscription_NULL;
            LIST_subscription_del list\lenght?lenght,index;
            RETURN item;
        ENDIF

        !Pop at last index, without lenght
        IF NOT Present(lenght) THEN
            FOR i FROM Dim(list,1) TO 1 DO
                IF list{i}<>subscription_NULL THEN
                    item:=list{i};
                    list{i}:=subscription_NULL;
                    RETURN item;
                ENDIF
            ENDFOR
        ENDIF

        !Pop at last index, with lenght
        item:=list{lenght};
        list{lenght}:=subscription_NULL;
        lenght:=lenght-1;
        RETURN item;

    ERROR
        RAISE ;
    ENDFUNC

    !Delete an item at a position of the list
    PROC LIST_subscription_del(
        INOUT subscription list{*}
        \INOUT num lenght,
        num index)

        list{index}:=subscription_NULL;

        !Shift down the above items to close gap, without lenght
        IF NOT Present(lenght) THEN
            FOR i FROM index TO Dim(list,1)-1 DO
                list{index}:=list{index+1};
            ENDFOR
            list{Dim(list,1)}:=subscription_NULL;
            RETURN ;
        ENDIF

        !Shift down the above items to close gap, with lenght
        FOR i FROM index TO lenght-1 DO
            list{index}:=list{index+1};
        ENDFOR
        list{lenght}:=subscription_NULL;

    ERROR
        RAISE ;
    ENDPROC

    !Returns the number of items actively used in the list
    FUNC num LIST_subscription_lenght(INOUT subscription list{*})
        VAR num lenght;
        lenght:=0;

        FOR i FROM 1 TO Dim(list,1) DO
            IF list{i}<>subscription_NULL THEN
                lenght:=lenght+1;
            ENDIF
        ENDFOR

        RETURN lenght;
    ERROR
        RAISE ;
    ENDFUNC

    !xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
    ! Examples
    !xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx

ENDMODULE